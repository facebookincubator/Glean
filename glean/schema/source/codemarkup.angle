# Copyright (c) Facebook, Inc. and its affiliates.

schema codemarkup.24 {
import src.1
import code.20
import codemarkup.buck.1
import codemarkup.cxx.1
import codemarkup.erlang.1
import codemarkup.flow.1
import codemarkup.hack.1
import codemarkup.haskell.1
import codemarkup.lsif.1
import codemarkup.pp.1
import codemarkup.python.1
import codemarkup.rust.1
import codemarkup.thrift.1
import codemarkup.types.1
import cxx1.4
import hack.6

#
# Public API
#

# Public language-agnostic API

# Associates an entity with a symbol kind.
# Note: available for partial indices where EntityInfo is incomplete.
predicate EntityKind:
  {
    entity: code.Entity,
    kind: codemarkup.types.SymbolKind,
  }
  { Entity, Kind } where
    codemarkup.python.PythonEntityKind { Entity, Kind } |
    codemarkup.cxx.CxxEntityKind { Entity, Kind } |
    codemarkup.pp.PpEntityKind { Entity, Kind } |
    codemarkup.hack.HackEntityKind { Entity, Kind } |
    codemarkup.erlang.ErlangEntityKind { Entity, Kind }

# Retrieve information about an entity
predicate EntityInfo:
  {
    entity: code.Entity,
    info: codemarkup.types.SymbolInfo,
  }
  { Entity, Info } where
    ( { kind = Kind, isAbstract = false } = Info;
      (codemarkup.python.PythonEntityKind { Entity, Kind } |
       codemarkup.pp.PpEntityKind { Entity, Kind} |
       codemarkup.erlang.ErlangEntityKind { Entity, Kind })
    ) |
    codemarkup.cxx.CxxEntityInfo { Entity, Info } |
    codemarkup.hack.HackEntityInfo { Entity, Info }

# All entities defined in a src.File and their locations
predicate FileEntityLocations:
  {
    file: src.File,
    location: codemarkup.types.Location,
    entity: code.Entity,
  }
  { File, Location, Entity } where
    ResolveLocation { Location, Entity };
    codemarkup.types.Location { file = File } = Location;

# All entities defined in a src.File, and their kinds
predicate FileEntityKinds:
  {
    file: src.File,
    entity: code.Entity,
    kind: codemarkup.types.SymbolKind,
  }
  { File, Entity, Kind } where
    FileEntityLocations { File, _, Entity };
    EntityKind { Entity, Kind };

predicate FileEntityInfos:
  {
    file: src.File,
    entity: code.Entity,
    info: codemarkup.types.SymbolInfo,
  }
  { File, Entity, Info } where
    FileEntityLocations { File, _, Entity };
    EntityInfo { Entity, Info };

# Map a location to the Entity declared there
predicate ResolveLocation:
  {
    location: codemarkup.types.Location,
    entity: code.Entity,
  }
  { Location, Entity } where
    codemarkup.hack.HackResolveLocation { Location, Entity } |
    codemarkup.python.PythonResolveLocation { Location, Entity } |
    codemarkup.flow.FlowResolveLocation { Location, Entity } |
    codemarkup.cxx.CxxResolveLocation { Location, Entity } |
    codemarkup.haskell.HaskellResolveLocation { Location, Entity } |
    codemarkup.rust.RustResolveLocation { Location, Entity } |
    codemarkup.buck.BuckResolveLocation { Location, Entity } |
    codemarkup.thrift.ThriftResolveLocation { Location, Entity } |
    codemarkup.erlang.ErlangResolveLocation { Location, Entity } |
    codemarkup.lsif.LsifResolveLocation { Location, Entity };

predicate EntityLocation:
  {
    entity: code.Entity,
    location: codemarkup.types.Location,
  }
  { Entity, Location } where
    codemarkup.hack.HackEntityLocation { Entity, Location } |
    codemarkup.python.PythonEntityLocation { Entity, Location } |
    codemarkup.flow.FlowEntityLocation { Entity, Location } |
    codemarkup.cxx.CxxEntityLocation { Entity, Location } |
    codemarkup.pp.PPEntityLocation { Entity, Location } |
    codemarkup.haskell.HaskellEntityLocation { Entity, Location } |
    codemarkup.rust.RustEntityLocation { Entity, Location } |
    codemarkup.buck.BuckEntityLocation { Entity, Location } |
    codemarkup.thrift.ThriftEntityLocation { Entity, Location } |
    codemarkup.erlang.ErlangEntityLocation { Entity, Location } |
    codemarkup.lsif.LsifEntityLocation { Entity, Location };

# Finding xrefs. All references in a file and their targets
predicate FileEntityXRefLocations:
  {
    file: src.File,
    xref: codemarkup.types.XRefLocation,
    entity: code.Entity,
  }
  { File, XRef, Entity } where
    codemarkup.hack.HackFileEntityXRefLocations { File, XRef, Entity } |
    codemarkup.flow.FlowFileEntityXRefLocations { File, XRef, Entity } |
    codemarkup.python.PythonFileEntityXRefLocations { File, XRef, Entity } |
    codemarkup.cxx.CxxFileEntityXRefLocations { File, XRef, Entity } |
    codemarkup.haskell.HaskellFileEntityXRefLocations { File, XRef, Entity } |
    codemarkup.rust.RustFileEntityXRefLocations { File, XRef, Entity } |
    codemarkup.buck.BuckFileEntityXRefLocations { File, XRef, Entity } |
    codemarkup.thrift.ThriftFileEntityXRefLocations { File, XRef, Entity } |
    codemarkup.erlang.ErlangFileEntityXRefLocations { File, XRef, Entity } |
    codemarkup.lsif.LsifFileEntityXRefLocations { File, XRef, Entity };

# Xrefs without the target location. This can be useful in cases
# where we don't have a target location, but we don't need it. This
# happens when indexing a subset of Hack files in a repo, for example.
predicate FileEntityXRefSpans:
  {
    file: src.File,
    span: src.ByteSpan,
    entity: code.Entity,
  }
  { File, Span, Entity } where
    codemarkup.hack.HackFileEntityXRefSpans { File, Span, Entity } |
    (
      codemarkup.flow.FlowFileEntityXRefLocations { File, XRef, Entity } |
      codemarkup.python.PythonFileEntityXRefLocations { File, XRef, Entity } |
      codemarkup.cxx.CxxFileEntityXRefLocations { File, XRef, Entity } |
      codemarkup.haskell.HaskellFileEntityXRefLocations { File, XRef, Entity } |
      codemarkup.rust.RustFileEntityXRefLocations { File, XRef, Entity } |
      codemarkup.buck.BuckFileEntityXRefLocations { File, XRef, Entity } |
      codemarkup.thrift.ThriftFileEntityXRefLocations { File, XRef, Entity } |
      codemarkup.erlang.ErlangFileEntityXRefLocations { File, XRef, Entity } |
      codemarkup.lsif.LsifFileEntityXRefLocations { File, XRef, Entity };
      { source = { span = Span }} = XRef
    )

# Finding xrefs. All references in a file, and their kinds
predicate FileEntityXRefKinds:
  {
    file: src.File,
    entity: code.Entity,
    kind: codemarkup.types.SymbolKind,
  }
  { File, Entity, Kind } where
    FileEntityXRefLocations { File, _, Entity };
    EntityKind { Entity, Kind };

predicate FileEntityXRefInfos:
  {
    file: src.File,
    entity: code.Entity,
    info: codemarkup.types.SymbolInfo,
  }
  { File, Entity, Info } where
    FileEntityXRefLocations { File, _, Entity };
    EntityInfo { Entity, Info };

# Find references to a language entity
# (deprecated in favor of more general codemarkup.EntityReferences)
predicate EntityUses:
  {
    target: code.Entity,
    file: src.File,
    span: src.ByteSpan
  }
  {Entity, File, Span} where
    codemarkup.hack.HackEntityUses { Entity, File, Span } |
    codemarkup.flow.FlowEntityUses { Entity, File, Span } |
    codemarkup.python.PythonEntityUses { Entity, File, Span } |
    codemarkup.cxx.CxxEntityUses { Entity, File, Span } |
    codemarkup.haskell.HaskellEntityUses { Entity, File, Span } |
    codemarkup.rust.RustEntityUses { Entity, File, Span } |
    codemarkup.buck.BuckEntityUses { Entity, File, Span } |
    codemarkup.erlang.ErlangEntityUses { Entity, File, Span };

# Find references to a language entity (same as EntityUses but with RangeSpans)
predicate EntityReferences:
  {
    target: code.Entity,
    file: src.File,
    range: codemarkup.types.RangeSpan
  }
  {Entity, File, RangeSpan} where
  (
    codemarkup.hack.HackEntityUses { Entity, File, Span } |
    codemarkup.flow.FlowEntityUses { Entity, File, Span } |
    codemarkup.python.PythonEntityUses { Entity, File, Span } |
    codemarkup.cxx.CxxEntityUses { Entity, File, Span } |
    codemarkup.haskell.HaskellEntityUses { Entity, File, Span } |
    codemarkup.rust.RustEntityUses { Entity, File, Span } |
    codemarkup.buck.BuckEntityUses { Entity, File, Span } |
    codemarkup.erlang.ErlangEntityUses { Entity, File, Span };
    { span = Span } = RangeSpan;
  ) | (
    codemarkup.lsif.LsifEntityUses { Entity, File, Range };
    { range = Range } = RangeSpan;
  )

#
# Annotations
#

predicate EntityAnnotations : code.Entity -> code.Annotations
  E -> A where
    CxxAnnotation{entity=E, anns=A} |
    codemarkup.hack.HackAnnotation{entity=E, anns=A}

predicate CxxAnnotation :
  {
    entity: code.Entity,
    anns: code.Annotations,
  }
  {{cxx={decl=D}}, {cxx={attributes=As}}} where
    ({ function_ = Decl } = D; cxx1.FunctionAttribute{declaration=Decl, attr=A}; As=[A]) |
    ({ function_ = Decl } = D; !cxx1.FunctionAttribute{declaration=Decl}; As=[]:[cxx1.Attribute.4])


#
# Relations
#

predicate ExtendsParentEntity :
  {
    child: code.Entity,
    parent: code.Entity
  }
  {C,P} where
    ({hack={decl={container=Child}}}=C; {hack={decl={container=Parent}}}=P;
      (
        hack.ContainerParent{container=Child, parent=Parent}
      )
    ) |
    ({cxx={decl={objcContainer=Child}}}=C; {cxx={decl={objcContainer=Parent}}}=P;
      (
        cxx1.ObjcImplements{implementation=Child, interface_=Parent} |
        cxx1.ObjcContainerBase{declaration=Child, base=Parent}
      )
    ) |
    ({cxx={decl={record_=Child}}}=C; {cxx={decl={record_=Parent}}}=P;
      (
        B[..]=cxx1.RecordBase{base=Parent}; cxx1.RecordDefinition{declaration=Child, bases=B}
      )
    )


predicate ExtendsChildEntity :
  {
    parent: code.Entity,
    child: code.Entity
  }
  {P,C} where
    ({hack={decl={container=Parent}}}=P; {hack={decl={container=Child}}}=C;
      (
        hack.ContainerChild{container=Parent, child=Child}
      )
    ) |
    ({cxx={decl={objcContainer=Parent}}}=P; {cxx={decl={objcContainer=Child}}}=C;
      (
        cxx1.ObjcInterfaceToImplementation{interface_=Parent, implementation=Child} |
        cxx1.ObjcContainerInheritance{base=Parent, declaration=Child}
      )
    ) |
    ({cxx={decl={record_=Parent}}}=P; {cxx={decl={record_=Child}}}=C;
      (
        cxx1.RecordDerived{base=Parent, derived=Child}
      )
    )

predicate ContainsChildEntity :
  {
    parent: code.Entity,
    child: code.Entity
  }
  { { hack = Parent }, { hack = Child } } where
    codemarkup.hack.HackContainsChildEntity { Parent, Child }

}
